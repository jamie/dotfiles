# .zprofile -- loaded at login, early
# Environment variables and commands which _don't_ need to be updated frequently
# eg. tool configuration, longer-running dependent configs
# `exec zsh --login` will re-run this file
# Probably XDG, ZDOTDIR handling should move here?

## Mac Browser
if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

## Editors
export PAGER='less'
if [[ -n $SSH_CONNECTION ]]; then
  # Remote
  export EDITOR='nano'
  export VISUAL='vim'
  export PAGER='less'
else
  # Local
  export EDITOR='code -w'
  export VISUAL='code'
  export PAGER='less'
fi

## Language
if [[ -z "$LANG" ]]; then
  export LANG='en_CA.UTF-8'
fi

## Paths

# homebrew
if [ -f ~/homebrew/bin/brew ]; then
  export HOMEBREW_PREFIX=~/homebrew
elif [ -f /usr/local/Homebrew/bin/brew ]; then
  export HOMEBREW_PREFIX=/usr/local/Homebrew
elif [ -f /opt/homebrew/bin/brew ]; then
  export HOMEBREW_PREFIX=/opt/homebrew
fi
export HOMEBREW_CASK_OPTS="--appdir=~/Applications"
export PKG_CONFIG_PATH=$PKG_CONFIG_PATH:$HOMEBREW_PREFIX/lib/pkgconfig

# asdf-vm
if [[ $HOMEBREW_PREFIX ]]; then
    export ASDF_DIR=$($HOMEBREW_PREFIX/bin/brew --prefix asdf)/libexec
    # TODO: export ASDF_PREFIX=$XDG_DATA_HOME/asdf
    export ASDF_PREFIX=$HOME/.asdf
fi

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
# Suffix (N) will skip adding the path if it does not exist.
path=(
  # Force higher priority
  $HOMEBREW_PREFIX/{,s}bin(N)
  $ASDF_PREFIX/shims(N)
  /usr/local/{,s}bin(N)

  $path

  # XDG Local bin added last, preventing shadowing
  $HOME/.local/bin
)

## Less
# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi
